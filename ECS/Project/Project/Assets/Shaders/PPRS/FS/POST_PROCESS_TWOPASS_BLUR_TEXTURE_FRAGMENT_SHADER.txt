#version 430 core

in vec4 texture_coordinates;

layout(binding = 0) uniform sampler2D TextureSampler0;

uniform bool horizontal;
uniform float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);

out vec4 color;

void main()
{
	vec2 TexCoords = texture_coordinates.xy;
	vec2 tex_offset = 1.0 / textureSize(TextureSampler0, 0); // gets size of single texel
    
	vec3 result = texture(TextureSampler0, TexCoords).rgb * weight[0]; // current fragment's contribution
    if(horizontal)
    {
        for(int i = 1; i < 5; ++i)
        {
            result += texture(TextureSampler0, TexCoords + vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
            result += texture(TextureSampler0, TexCoords - vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
        }
    }
    else
    {
        for(int i = 1; i < 5; ++i)
        {
            result += texture(TextureSampler0, TexCoords + vec2(0.0, tex_offset.y * i)).rgb * weight[i];
            result += texture(TextureSampler0, TexCoords - vec2(0.0, tex_offset.y * i)).rgb * weight[i];
        }
    }
	
	color = vec4(result.xyz, 1.0);
}